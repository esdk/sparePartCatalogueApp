plugins {
	id 'com.gradle.build-scan' version '2.0.2'
	id "java"
	id "eclipse"
	id "maven-publish"
	id "de.abas.esdk" version "0.12.0"
	id "com.dorongold.task-tree" version "1.3.1"
}

buildScan {
	termsOfServiceUrl = 'https://gradle.com/terms-of-service'
	termsOfServiceAgree = 'yes'
}

repositories {
	maven { url "http://$NEXUS_HOST:$NEXUS_PORT/nexus/content/repositories/$NEXUS_NAME-SNAPSHOT" }
	maven { url "http://$NEXUS_HOST:$NEXUS_PORT/nexus/content/repositories/$NEXUS_NAME" }
}

esdk {
	app {
		name = "sparePartsCatalogueApp"
		vendorId = "ag"
		appId = "spare"
		shared = false
		infosystems = ["IS.OW1.REPLACEMENTCATALOGUE"]
		tables = ["Replacement"]
		enums = ["FILE_FORMAT"]
		essentialsVersions = ["2016r4n13-2016r4n16", "2017r1n00-2017r4n16"]
		preconditions = ["workDirs=ow1"]
		workdirs = [ "ow1" ]
		languages = "DEA"
	}

	abas {
		homeDir = ABAS_HOMEDIR
		clientDir = ABAS_CLIENTDIR
		clientId = ABAS_CLIENTID
		edpHost = EDP_HOST
		edpPort = EDP_PORT.toInteger()
		edpUser = EDP_USER
		edpPassword = EDP_PASSWORD
	}

	nexus {
		nexusHost = NEXUS_HOST
		nexusPort = NEXUS_PORT.toInteger()
		nexusRepoName = NEXUS_NAME
		nexusPassword = NEXUS_PASSWORD
	}

	ssh {
		host = SSH_HOST
		port = SSH_PORT.toInteger()
		user = SSH_USER
		password = SSH_PASSWORD
		key = SSH_KEY
	}
	installType = "SSH"
}

gradle.taskGraph.whenReady {
	esdkAppDocumentation {
		attributes 'productVersion': version.split("-")[0]
	}
}

configurations {
	installer
}

configurations.installer {
	resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
}

group = 'de.abas.esdk.app'

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId 'de.abas.esdk.app'
			artifactId 'sparePartCatalogue'
			artifact createAppJar
		}
	}
	repositories {
		if (project.hasProperty("esdkSnapshotURL") && project.hasProperty("esdkReleaseURL")
				&& project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")) {
			if (version.endsWith('-SNAPSHOT')) {
				maven {
					url esdkSnapshotURL
					credentials {
						username nexusUser
						password nexusPassword
					}
				}
			} else {
				maven {
					url esdkReleaseURL
					credentials {
						username nexusUser
						password nexusPassword
					}
				}
			}
		}
	}
}

dependencies {
	provided 'de.abas.homedir:log4j:1.0.0'
	provided "de.abas.homedir:abas-db-base:1.0.0"
	provided 'de.abas.homedir:jedp:1.0.0'
	provided "de.abas.homedir:abas-jfop-runtime-api:1.0.0"
	provided "de.abas.homedir:abas-jfopapi:1.0.0"
	provided "de.abas.homedir:abas-erp-common:1.0.0"
	provided "de.abas.homedir:abas-enums:1.0.0"

	implementation "de.abas.homedir:abas-jfop-base:1.0.0"
	implementation "de.abas.homedir:abas-db-util:1.0.0"
	implementation "de.abas.homedir:abas-axi2:1.0.0"
	implementation "de.abas.homedir:abas-axi:1.0.0"
	implementation "de.abas.homedir:abas-db-internal:1.0.0"
	implementation "de.abas.homedir:jcl-over-slf4j:1.0.0"
	implementation "de.abas.homedir:slf4j-api:1.0.0"
	implementation "de.abas.clientdir:abas-db:1.0.0-SNAPSHOT"
	implementation "de.abas.clientdir:abas-db-infosys:1.0.0-SNAPSHOT"

	runtime "de.abas.homedir:commons-collections:1.0.0"

	testCompile 'junit:junit:4.12'
	testCompile 'org.hamcrest:hamcrest-all:1.3'
}
