plugins {
    id "java"
    id "eclipse"
    id "maven-publish"
    id "de.abas.esdk" version "1.2.6"
}

repositories {
    exclusiveContent {
        forRepository {
            flatDir {
                // mapped directory containing homedir libs
                dirs 'erpsync/homedir/lib'
            }
        }
        filter {
            // restrict this repository to lookup homedir libs only
            includeGroup('de.abas.homedir')
        }
    }
    exclusiveContent {
        forRepository {
            flatDir {
                // mapped directory containing clientdir libs
                dirs 'erpsync/clientdir/lib'
            }
        }
        filter {
            // restrict this repository to lookup clientdir libs only
            includeGroup('de.abas.clientdir')
        }
    }
    maven {
        url "https://registry.abas.sh/artifactory/abas.maven-public/"
    }
}

gradle.taskGraph.whenReady {
    esdkAppDocumentation {
        attributes 'productVersion': version.split("-")[0]
    }
}

configurations {
    installer
}

configurations.installer {
    resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
}

group = 'de.abas.esdk.app'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'de.abas.esdk.app'
            artifactId 'sparePartCatalogue'
            artifact createAppJar
        }
    }
    repositories {
        if (project.hasProperty("esdkSnapshotURL") && project.hasProperty("esdkReleaseURL")
                && project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")) {
            if (version.endsWith('-SNAPSHOT')) {
                maven {
                    url esdkSnapshotURL
                    credentials {
                        username nexusUser
                        password nexusPassword
                    }
                }
            } else {
                maven {
                    url esdkReleaseURL
                    credentials {
                        username nexusUser
                        password nexusPassword
                    }
                }
            }
        }
    }
}

// Always generate code for Java 8
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

dependencies {
    constraints {
        implementation("org.apache.logging.log4j:log4j-core") {
            version {
                strictly("[2.17, 3[")
                prefer("2.17.1")
            }
            because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: " +
                    "Log4j vulnerable to remote code execution and other critical security vulnerabilities")
        }
    }
    provided 'de.abas.homedir:log4j:1.0.0'
    provided "de.abas.homedir:abas-db-base:1.0.0"
    provided 'de.abas.homedir:jedp:1.0.0'
    provided "de.abas.homedir:abas-jfop-runtime-api:1.0.0"
    provided "de.abas.homedir:abas-jfopapi:1.0.0"
    provided "de.abas.homedir:abas-erp-common:1.0.0"
    provided "de.abas.homedir:abas-enums:1.0.0"

    implementation "de.abas.homedir:abas-jfop-base:1.0.0"
    implementation "de.abas.homedir:abas-db-util:1.0.0"
    implementation "de.abas.homedir:abas-axi2:1.0.0"
    implementation "de.abas.homedir:abas-axi:1.0.0"
    implementation "de.abas.homedir:abas-db-internal:1.0.0"
    implementation "de.abas.homedir:jcl-over-slf4j:1.0.0"
    implementation "de.abas.homedir:slf4j-api:1.0.0"
    implementation "de.abas.clientdir:abas-db:1.0.0-SNAPSHOT"
    implementation "de.abas.clientdir:abas-db-index:1.0.0-SNAPSHOT"
    implementation "de.abas.clientdir:abas-db-infosys:1.0.0-SNAPSHOT"

    implementation "de.abas.homedir:commons-collections:1.0.0"

    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "de.abas.esdk.test.util:esdk-test-utils:0.0.9"
    testImplementation "org.mockito:mockito-all:1.10.19"
    testImplementation "org.powermock:powermock-module-junit4:1.6.2"
    testImplementation "org.powermock:powermock-api-mockito:1.6.2"
}
